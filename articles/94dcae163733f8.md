---
title: "LittleFSでM5Stackにお手軽データ保存"
emoji: "📖"
type: "tech"
topics: ["m5stack","esp32","arduino","platformio"]
published: true
---

みんな、こんにちは！M5Stackのようなマイコンにちょっとしたデータを保存して読み込みたいことってあるよね？あとM5StickシリーズのようにmicroSDカードスロットを持たないマイコンも存在しているし、そんな時にどうやってマイコンにデータを置くの？って疑問もあるよね。そんなみんなのために、M5Stackのほとんどのマイコンモジュールに使われているESPというマイコン（ESP32/ESP8266、以下ESP）にはフラッシュメモリ上で簡単にファイルを扱える仕組みがあるよ。LittleFSって言うんだけど、LittleFSを使うことでお手軽にマイコンのフラッシュにデータを保存して読み込むことができるよ。今回はESPで使えるファイルシステム「LittleFS」について使い方をまとめているよ。

他の記事でもLittleFSの使い方は説明しているけど、何度も参照しそうな情報だから改めてまとめておくよ。
https://zenn.dev/nananauno/articles/62d95c3ccad989#littlefs

## やること
今回はmicroSDカードスロットの無いM5Stickシリーズを使って、LittleFS上に保存されたテキスト、画像、音声データを読み込んで表示と再生をしてみるよ。ボクはM5StickS3を使ったけど、好きなマイコンでやってみてね。

作ったサンプルの動作はこんな感じ：
・LittleFS上に適当な画像(png)、音声(wav)、テキストを配置
・M5StickS3が起動したらLittleFS上の画像とテキストを読み込んで画面に表示
・ボタンAを押すとLittleFS上の音声を再生

https://x.com/nananauno/status/2023008904947564657?s=20

## 環境
今回ボクが使った環境は以下の通りだよ。

**マイコンモジュール**
- M5StickS3

**Arduino**
- Arduino IDE 2.x
- arduino-littlefs-upload 1.6.3
- Board: M5Stack 3.2.5

**PlatformIO**
- pioarduino (platform-espressif32) Arduino v3.3.7 / ESP-IDF v5.5.2.260206

## 対象読者
- M5Stackにお手軽にデータを保存して読み込みたい
- LittleFSの使い方をおさらいしたい

## LittleFSってなに？

LittleFSはESPでお手軽にフラッシュメモリ上にデータを保存できる仕組みだよ。容量はフラッシュのサイズとパーティションの設定に依存するけど、8MBのフラッシュで1.5MBぐらいがデフォルトで使えるようになっているよ。そこまでサイズは大きくないけど、ちょっとしたテキストや画像、音声ぐらいなら十分に保存することができるよね。

例えば、こんなことに使える…
- センサーの測定値をログとして残したい
- Webサーバー用のHTMLファイルを保存したい
- 画面に表示する画像を保持したい

LittleFSで使える領域は使用しているパーティションテーブルによってサイズが変わってくるよ。パーティションテーブルはよく使われるものはいくつか定義されているからその中から使うこともできるし、自分で定義することもできるよ。パーティションテーブルのお話はここでは省略するから興味があったら以下の資料を参考に設定してみてね。

よく使われるテーブルは予め定義されているよ：
https://github.com/espressif/arduino-esp32/tree/master/tools/partitions

パーティションテーブルの設定方法はこちらを見てね：
https://docs.espressif.com/projects/esp-idf/en/stable/esp32/api-guides/partition-tables.html

### SPIFFSと何が違うの？

SPIFFSはLittleFSよりも前に使われていた仕組み。できることはほぼ同じなんだけどLittleFSの方が堅牢性が高いから今は非推奨になっているよ。SPIFFSを使っているコードをビルドしようとすると、こんな警告が出るようになってるよね？ネット上の古いサンプルでSPIFFSが使われていたりするけど、これからはLittleFSを使った方が良さそうだね。

```
> SPIFFS has been deprecated. Please consider moving to LittleFS or other filesystems.
```

### NVSと何が違うの？

NVSという名前も聞いたことあるかな？フラッシュに値を保持する方法としてNVSという仕組みもあるんだけど、NVSがLittleFSと違うところは、NVSはキー・バリュー形式ってことと保存できる容量は数KBということだよ。キー・バリューというのは、指定したキーに対して値（バリュー）を保存したり読み込んだりする方法で、長いテキストや画像、音声の保持には向いていないよね。設定値やWifiのSSIDとかパスワードのような短い値を保持することが主目的になっているよ。

今回はNVSの使い方は説明しないけど、また別の機会に説明できるといいね。

## LittleFS領域にデータを置いてみよう

LittleFS領域にデータを置くにはあらかじめdataフォルダを作成してその中にマイコンのフラッシュにアップロードしたいファイルを全て置いておくよ。dataフォルダの位置やアップロード方法はArduinoとPlatformIOで異なるからそれぞれの方法を説明しているよ。

作ったアプリとLittleFS領域へのデータ書き込みは以下の順番で実行してね。既に書き込み済みのアプリが使っているパーティションテーブルが全く同じであれば問題は起きないと思うけど、違うパーティションテーブルが使われているとうまく書き込めない原因になるよ。
1. アプリ領域へ書き込み
1. LittleFS領域へ書き込み

### Arduinoから

**事前準備**
Arduino IDEからLittleFS領域に書き込むためにはプラグインを入れておく必要があるよ。以下のリポジトリを参考に入れてみてね。

https://github.com/earlephilhower/arduino-littlefs-upload

プラグインが入ったらスケッチのフォルダを開いてxx.inoがある場所にdataフォルダを作成するよ。そして、dataフォルダに書き込みたいデータを配置してね。

**アップロード**
MacならCmd+Shift+P、WinならCtrl+Shift+Pを押して、コマンドのところで`Upload LittleFS to Pico/ESP8266/ESP32`を実行するとフラッシュにデータをアップロードすることができるよ。

:::message
Serial Monitorは閉じておかないとエラーになるよ。
:::

:::message
"ERROR: No port specified, check IDE menus."と表示された時はESPのブートモードを解除（リセット）してArduino IDEも再起動してみよう。
:::

### PlatformIOから

**事前準備**
PlatformIOでLittleFS領域に書き込むには、プロジェクトのルート（srcと同じ階層）に`data`フォルダを作成するよ。そして、dataフォルダに書き込みたいデータを配置してね。

**アップロード**
vscodeの左のサイドバーからPlatformIOのアイコンを選んで、PROJECT TASKSにある`Upload Filesystem Image`を選択するとマイコンのフラッシュにアップロードすることができるよ。M5StickS3はブートモードのままで書き込みができたよ。

## 作ってみよう

じゃあ実際にLittleFSに書き込んだデータはどうやって使うの？というのをここから説明していくよ。サンプルコードでは以下の３つのデータをLittleFS領域から読み込んで表示や再生を行っているよ。

- 画像
- テキスト
- 音声

M5Stackを使っているから画面表示や音声の再生はM5Unifiedを使っているよ。

### LittleFSの初期化
LittleFSを使用するには`LittleFS.h`をインクルードする必要があるよ。**M5Unifiedを使用する場合は M5Unified.h よりも前にインクルードするのがポイント。** 順序が逆だとビルド時にエラーになるよ。

**LittleFS.begin()**
LittleFSを初期化するよ。引数に`true`を付けるとLittleFS領域がフォーマットされていない場合に自動的にフォーマットしてくれるよ。dataフォルダからデータをアップロードして使用する分にはtrueを付けなくても大丈夫なはず（付けない場合はfalse）だけど、マウントに失敗するようならtrueにしてみてね。

### 画像(PNG)の読み込みと表示
M5Unifiedを使えばLittleFSからの画像読み込みと表示はとっても簡単！drawPngFileを呼び出すだけでOKだよ。

**M5.Display.drawPngFile(LittleFS, "/xxx.png", 0, 0)**
LittleFSの/xxx.pngを読み込んで画面に表示するよ。0,0は画像を描画する位置x,yだよ。BMP、JPEGもdrawBmpFile, drawJpgFileを使うことで読み込みと表示が可能だよ。ここでは指定するLittleFS上のパスは/（スラッシュ）から始める必要があるよ。

M5Unifiedで使える画像描画の関数はこちらを参考にしてね。
https://docs.m5stack.com/ja/arduino/m5gfx/m5gfx_image

### テキストの読み込みと表示
テキストファイルを読み込む方法は一般的なファイルの読み込みとほぼ同じやり方だよ。LittleFSから指定されたpathでファイルを読み込んで、読み込んだテキストを返す例はこんな感じ。`readString()`を呼び出すことでファイル全体からテキストを読み込むことができるよ。

```cpp
String readTextFile(const char* path) {
  File f = LittleFS.open(path);
  if (!f) return "Not found";
  String s = f.readString();
  f.close();
  return s;
}
```

### 音声(wav)の読み込みと再生
M5Unifiedを使うと音声ファイルを再生するのもとっても簡単！LittleFSからwavファイルのデータを読み込んでそれをM5UnifiedのplayWavに渡すだけだよ。LittleFSからwavファイルを読み込む例はこんな感じ。`readBytes`を使ってファイルのバイナリデータを全て読み込んでいるよ。

```cpp
size_t loadWavFromFS(const char* path) {
  File wavFile = LittleFS.open(path, "r");
  if (!wavFile) {
    log_e("Failed to open WAV file: %s", path);
    return 0;
  }

  size_t fileSize = wavFile.size();
  wavdata = new uint8_t[fileSize];
  wavFile.readBytes((char*)wavdata, fileSize);
  wavFile.close();
  return fileSize;
}
```

M5Unifiedで音声ファイルを再生するには読み込んだwavのデータとデータのサイズを渡すだけでOKだよ。
```cpp
M5.Speaker.playWav(wavdata, wavdata_size);
```

### サンプルコード
画像、テキスト、音声を読み込んで表示したり再生したりする全体のコードはこんな感じだよ。

```cpp
#include <LittleFS.h>   // 必ずM5Unifiedよりも先にインクルードする
#include <M5Unified.h>
#include <Preferences.h>

//Wavデータを保持する
const uint8_t *wavdata;
size_t wavdata_size=0;

// LittleFSから指定されたファイル名のWAVファイルを読み込む
size_t loadWavFromFS(const char* path) {
  File wavFile = LittleFS.open(path, "r");
  if (!wavFile) {
    log_e("Failed to open WAV file: %s", path);
    return 0;
  }

  size_t fileSize = wavFile.size();
  wavdata = new uint8_t[fileSize];
  wavFile.readBytes((char*)wavdata, fileSize);
  wavFile.close();
  return fileSize;
}

// LittleFSから指定されたファイル名のテキストファイルを読み込む
String readTextFile(const char* path) {
  File f = LittleFS.open(path);
  if (!f) return "Not found";
  String s = f.readString();
  f.close();
  return s;
}

void setup() {
  M5.begin();

  M5.Power.setExtOutput(false);
  M5.Speaker.setVolume(178); // スピーカー音量設定
  
  // LittleFSの初期化
  if (!LittleFS.begin()) {
    log_e("LittleFS mount failed");
    return;
  }

  // 画面表示の設定
  M5.Display.setRotation(1);  // 画面を横向きに設定
  M5.Display.fillScreen(BLACK);
  M5.Display.setTextColor(WHITE, BLACK);
  M5.Display.setCursor(0, 0);
  M5.Display.setTextSize(2);

  // PNGファイルをLittleFSから読み込み画面に表示
  M5.Display.drawPngFile(LittleFS, "/teto.png", 0, 0);

  // テキストファイルをLittleFSから読み込み画面に表示
  String text = readTextFile("/teto.txt");
  M5.Display.println(text);

  // WAVファイルをLittleFSから読み込み
  wavdata_size = loadWavFromFS("/teto1.wav");
  if(wavdata_size == 0) {
    log_e("Failed to load WAV file");
  }else{
    log_i("Loaded WAV file: %u bytes", wavdata_size);
  }  
}

void loop() {
  M5.update();
  
  if (M5.BtnA.wasPressed()) { 
    // ボタンA押下で音声を再生
    bool result = M5.Speaker.playWav(wavdata, wavdata_size);
    if(!result){
      log_e("playWav failed");
    }else{
      log_i("playWav started");
    }
  }
}
```

**Arduinoのボード設定**
M5StickS3を使った場合のArduinoのボード設定を参考までに載せておくよ。特にデフォルトから変更していないけど、うまく動かないときはチェックしてみてね。

USB CDC On Boot: "Enabled"
CPU Frequency: "240MHz (WiFi)"
Core Debug Level: "None"
USB DFU On Boot: "Disabled"
Erase All Flash Before Sketch Upload: "Disabled"
Events Run On: "Core 1"
Flash Mode: "QIO 80MHz"
Flash Size: "8MB (64Mb)"
JAG Adapter: "Disabled"
Arduino Runs On: "Core 1"
USB Firmware MSC On Boot: "Disabled"
Partition Scheme: "8M with spiffs (3MB APP/1.5MB SPIFFS)"
PSRAM: "OPI PSRAM"
Upload Mode: "UARTO / Hardware CDC"
Upload Speed: "921600"
USB Mode: "Hardware CDC and JTAG"

Partition Schemeにspiffsって入っているけど、このままでLittleFSも使えるよ。

**Platformio.ini**
M5StickS3を使った場合のplatformio.iniの設定を参考までに載せておくよ。

```ini
[env]
;platform = espressif32
platform = https://github.com/pioarduino/platform-espressif32.git
framework = arduino
lib_deps =
    M5Unified=https://github.com/m5stack/M5Unified

[env:esp32-s3-devkitc-1]
board = esp32-s3-devkitc-1
board_build.arduino.partitions = default_8MB.csv
board_build.arduino.memory_type = qio_opi
board_build.filesystem = littlefs
build_flags =
    -DESP32S3
    -DBOARD_HAS_PSRAM
    -mfix-esp32-psram-cache-issue
    -DCORE_DEBUG_LEVEL=5
    -DARDUINO_USB_CDC_ON_BOOT=1
    -DARDUINO_USB_MODE=1
lib_deps =
    ${env.lib_deps}
    M5PM1=https://github.com/m5stack/M5PM1
```

## まとめ
今回はESPでお手軽にフラッシュにデータを保持できるLittleFSについてまとめたよ。M5StackだとM5Unifiedと一緒に使用することでとっても簡単にLittleFSからデータを読み込んで画面に表示したり音声の再生をすることができたね。microSDが付いていないマイコンやちょっとしたデータを扱いたいときにはとっても便利な仕組みだよね。データの書き込みもdataフォルダを作ってアップロードするだけだから容量さえ気をつければ簡単にデータを持ち運びできそうだね！お手軽にフラッシュにデータを保存できるLittleFSをぜひ活用して色々と作ってみてね！

じゃあ、またね！
